buildscript {
   repositories {
      maven { url = "https://repo.gradle.org/gradle/libs-releases-local" }
      maven { url "https://plugins.gradle.org/m2/" }
      mavenLocal()
   }
   dependencies {
      classpath "gradle.plugin.us.ihmc:ihmc-build:0.4.10"
      classpath "org.gradle:gradle-kotlin-dsl:0.10.3"
   }
}
plugins {
   id "org.hidetake.ssh" version "2.9.0"
}
apply plugin: "us.ihmc.ihmc-build"

ihmc.configureProjectForOpenRobotics(project)

dependencies {
   compile group: 'us.ihmc', name: 'euclid-core', version: '0.4.5'
   compile group: 'us.ihmc', name: 'IHMCRealtime', version: '1.1.8'
   compile group: 'us.ihmc', name: 'ihmc-yovariables', version: '0.1.0'
   compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
   compile group: 'us.ihmc.thirdparty.jinput', name: 'jinput', version: '160414'
   compile group: 'org.ejml', name: 'dense64', version: '0.30'
   compile group: 'org.ejml', name: 'core', version: '0.30'
   compile group: 'us.ihmc.thirdparty.jme', name: 'jme3-core', version: '3.1.0-internal4'
   compile group: 'com.martiansoftware', name: 'jsap', version: '2.1'
   compile group: 'org.bidib.jbidib.org.qbang.rxtx', name: 'rxtxcomm', version: '2.2'
   compile group: 'us.ihmc', name: 'ihmc-commons', version: '0.11.0-alpha'
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-communication", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-java-extensions", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-humanoid-robotics", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-state-estimation", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "common-walking-control-modules", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-avatar-interfaces", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-jmonkey-engine-toolkit", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "model-file-loader", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "sensor-processing", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "simulation-construction-set", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-robot-data-logger", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-perception", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-whole-body-controller", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-java-toolkit", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-robotics-toolkit", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-robot-description", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-robot-models", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-robot-data-visualizer", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-graphics-description", dependencyMode)
}

testDependencies {
   compile group: 'junit', name: 'junit', version: '4.11'
   compile group: 'us.ihmc', name: 'ihmc-continuous-integration-framework', version: '0.9.4'
   compile group: 'us.ihmc', name: 'ihmc-commons-testing', version: '0.11.0-alpha'
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-avatar-interfaces-test", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-robotics-toolkit-test", dependencyMode)
   compile ihmc.getBuildVersion("us.ihmc", "ihmc-java-extensions-test", dependencyMode)
}

remotes {
   stepprController {
      host = '10.66.171.20' //CPU1
      // Set the username and password in ~/.gradle/gradle.properties.
      user = steppr_username
      password = steppr_password
      knownHosts = allowAnyHosts
   }
   wandererController {
      host = '10.66.171.21'
      // Set the username and password in ~/.gradle/gradle.properties.
      user = wanderer_username
      password = wanderer_password
      knownHosts = allowAnyHosts
   }
}

task deploy(dependsOn: assemble) << {
   def rep;
   def scriptDir;
   def directory;

   if (robot == "steppr")
   {
      rep = remotes.stepprController
      scriptDir = new File("scripts/steppr");
      directory = "steppr"
   } else if (robot == "wanderer")
   {
      rep = remotes.wandererController
      scriptDir = new File("scripts/wanderer");
      directory = "wanderer"
   } else
   {
      throw new GradleException("Invalid robot \"$robot\" passed in. Valid options are \"steppr\" and \"wanderer\"");
   }

   println "Trying ssh"

   ssh.run {
      session(rep) {
         execute 'mkdir -p ' + directory + '/lib'
         println "Adding Jars"
         configurations.runtime.each {
            put it, directory + '/lib'
         }

         put jar.archivePath, directory + '/AcsellController.jar'

         def scriptDirCollection = files { scriptDir.listFiles() }
         put scriptDirCollection, directory

         scriptDirCollection.each {
            execute 'chmod 777 ' + directory + '/' + it.name
         }
      }
   }
}

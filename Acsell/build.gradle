buildscript {
   repositories {
      maven { url = "https://repo.gradle.org/gradle/libs-releases-local" }
      maven { url "https://plugins.gradle.org/m2/" }
      mavenLocal()
   }
   dependencies {
      classpath "gradle.plugin.us.ihmc:ihmc-build:0.5.0"
      classpath "org.gradle:gradle-kotlin-dsl:0.10.3"
   }
}
plugins {
   id "org.hidetake.ssh" version "2.9.0"
}
apply plugin: "us.ihmc.ihmc-build"

ihmc {
   loadRepositoryWideProperties(projectDir.toPath().parent.resolve("product.properties"))
   
   configureDependencyResolution()
   configurePublications()
}

testSuites {
   bambooPlanKeys = ["LIBS-UI2", "LIBS-FAST2", "LIBS-FLAKY2", "LIBS-SLOW2", "LIBS-VIDEO2", "LIBS-INDEVELOPMENT2"]
   disableBambooConfigurationCheck = true
}

mainDependencies {
   compile group: 'us.ihmc', name: 'euclid-core', version: '0.4.5'
   compile group: 'us.ihmc', name: 'IHMCRealtime', version: '1.1.8'
   compile group: 'us.ihmc', name: 'ihmc-yovariables', version: '0.1.0'
   compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
   compile group: 'us.ihmc.thirdparty.jinput', name: 'jinput', version: '160414'
   compile group: 'org.ejml', name: 'dense64', version: '0.30'
   compile group: 'org.ejml', name: 'core', version: '0.30'
   compile group: 'us.ihmc.thirdparty.jme', name: 'jme3-core', version: '3.1.0-internal4'
   compile group: 'com.martiansoftware', name: 'jsap', version: '2.1'
   compile group: 'org.bidib.jbidib.org.qbang.rxtx', name: 'rxtxcomm', version: '2.2'
   compile group: 'us.ihmc', name: 'ihmc-commons', version: '0.11.0-alpha'
   compile group: "us.ihmc", name: "ihmc-communication", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-java-extensions", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-humanoid-robotics", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-state-estimation", version: dependencyMode
   compile group: "us.ihmc", name: "common-walking-control-modules", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-avatar-interfaces", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-jmonkey-engine-toolkit", version: dependencyMode
   compile group: "us.ihmc", name: "model-file-loader", version: dependencyMode
   compile group: "us.ihmc", name: "sensor-processing", version: dependencyMode
   compile group: "us.ihmc", name: "simulation-construction-set", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-robot-data-logger", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-perception", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-whole-body-controller", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-java-toolkit", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-robotics-toolkit", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-robot-description", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-robot-models", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-robot-data-visualizer", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-graphics-description", version: dependencyMode
}

testDependencies {
   compile group: 'junit', name: 'junit', version: '4.11'
   compile group: 'us.ihmc', name: 'ihmc-continuous-integration-framework', version: '0.9.4'
   compile group: 'us.ihmc', name: 'ihmc-commons-testing', version: '0.11.0-alpha'
   compile group: "us.ihmc", name: "ihmc-avatar-interfaces-test", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-robotics-toolkit-test", version: dependencyMode
   compile group: "us.ihmc", name: "ihmc-java-extensions-test", version: dependencyMode
}

remotes {
   stepprController {
      host = '10.66.171.20' //CPU1
      // Set the username and password in ~/.gradle/gradle.properties.
      user = steppr_username
      password = steppr_password
      knownHosts = allowAnyHosts
   }
   wandererController {
      host = '10.66.171.21'
      // Set the username and password in ~/.gradle/gradle.properties.
      user = wanderer_username
      password = wanderer_password
      knownHosts = allowAnyHosts
   }
}

task deploy(dependsOn: assemble) << {
   def rep;
   def scriptDir;
   def directory;

   if (robot == "steppr")
   {
      rep = remotes.stepprController
      scriptDir = new File("scripts/steppr");
      directory = "steppr"
   } else if (robot == "wanderer")
   {
      rep = remotes.wandererController
      scriptDir = new File("scripts/wanderer");
      directory = "wanderer"
   } else
   {
      println("Invalid robot \"$robot\" passed in. Valid options are \"steppr\" and \"wanderer\"");
   }

   println "Trying ssh"

   ssh.run {
      session(rep) {
         execute 'mkdir -p ' + directory + '/lib'
         println "Adding Jars"
         configurations.runtime.each {
            put it, directory + '/lib'
         }

         put jar.archivePath, directory + '/AcsellController.jar'

         def scriptDirCollection = files { scriptDir.listFiles() }
         put scriptDirCollection, directory

         scriptDirCollection.each {
            execute 'chmod 777 ' + directory + '/' + it.name
         }
      }
   }
}

apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
version = '0.7.0'

rootProject.ext.vcsUrl = "https://bitbucket.org/ihmcrobotics/ihmc-open-robotics-software.git"
rootProject.ext.snapshotCounter = 1
rootProject.ext.releaseCandidateCounter = 1

getGitInfo()

getDate()

if(buildType.equals("NIGHTLY"))
{
  version += "-NIGHTLY+date.${project.ext.date}.rev.${project.ext.revision}"
}

if(buildType.equals("SNAPSHOT"))
{
  version += "-SNAPSHOT${project.ext.snapshotCounter}.rev.${project.ext.revision}"
}

if(buildType.equals("RC"))
{
  version += "-RC${project.ext.releaseCandidateCounter}.rev.${project.ext.revision}"
}

if(buildType.equals("RELEASE"))
{
  if(!rootProject.ext.branch.equals("master"))
  {
      throw new GradleException("Cannot make RELEASE build from branch \"${rootProject.ext.branch}\", it is not the branch used for stable releases!")
  }
}

buildscript {
  repositories {
    mavenLocal()
	  jcenter()
	  mavenCentral()
  }
}

repositories {
  mavenLocal()

  maven {
    url "https://bengal.ihmc.us/nexus/content/repositories/releases/"
  }

  maven {
      url "https://bengal.ihmc.us/nexus/content/repositories/thirdparty/"
  }

  maven {
      url "https://bengal.ihmc.us/nexus/content/repositories/central/"
  }

  jcenter()

  mavenCentral()
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'

  buildscript {
    repositories {
      mavenLocal()
  	  jcenter()
  	  mavenCentral()
    }
  }

  jar {
    from (project.projectDir) {
      include 'LICENSE*txt'
    }
  }

  task sourceJar(type: Jar) {
    from sourceSets.main.allJava
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {

        groupId 'us.ihmc'
        artifactId "${project.name}"
        version "${rootProject.version}"

        from components.java

        artifact sourceJar {
          classifier "sources"
        }

        pom.withXml {
          def licenseNode = asNode().appendNode('licenses').appendNode('license')
          licenseNode.appendNode('name', project.ext.licenseName)
          licenseNode.appendNode('url', project.ext.licenseURL)
        }
      }
    }
  }
}

def getGitInfo() {
  getGitHash()
  getGitBranch()
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy.MM.dd')
    rootProject.ext.date = formattedDate
}

def getGitHash() {
  def cmd = "git rev-parse --short HEAD"
  def proc = cmd.execute()
  rootProject.ext.revision = proc.text.trim()
}

def getGitBranch() {
  def cmd = "git rev-parse --abbrev-ref HEAD"
  def proc = cmd.execute()
  rootProject.ext.branch = proc.text.trim()
}
